<html>
<head>

<title>WebGL</title>
<meta charset="utf-8">
<link rel='stylesheet' type='text/css' href='css/site.css' />
<script src="js/lib/underscore.js"></script>
<script src="js/lib/domready.js"></script>
<script src="js/lib/glmatrix.js"></script>
<script src="js/lib/webgl-debug.js"></script>
<script src="js/lib/bean.js"></script>
<script src="js/lib/dat.gui.js"></script>
<script src="js/util.js"></script>
<script src="js/hash.js"></script>
<script src="js/debug.js"></script>
<script src="js/mrucache.js"></script>
<script src="js/protobuf.js"></script>
<script src="js/glutil.js"></script>
<script src="js/linegeometry.js"></script>
<script src="js/pointgeometry.js"></script>
<script src="js/fillbuffer.js"></script>
<script src="js/vertexbuffer.js"></script>
<script src="js/tile.js"></script>
<script src="js/transform.js"></script>
<script src="js/parse_style.js"></script>
<script src="js/style.js"></script>
<script src="js/coordinate.js"></script>
<script src="js/interaction.js"></script>
<script src="js/debug_text.js"></script>
<script src="js/dispatcher.js"></script>
<script src="js/labeltexture.js"></script>
<script src="js/images.js"></script>
<script src="js/map.js"></script>
<script src="js/painter.js"></script>
<script src="js/site.js"></script>

<!-- these are the previous shaders -->
<script id="debug-vertex" type="x-shader/x-vertex">
attribute vec2 a_pos;

uniform float u_pointsize;
uniform mat4 u_posmatrix;

void main() {
    gl_Position = u_posmatrix * vec4(a_pos, step(32767.0, a_pos.x), 1);
    gl_PointSize = u_pointsize;
}
</script>
<script id="debug-fragment" type="x-shader/x-fragment">
precision mediump float;

uniform vec4 u_color;

void main() {
    gl_FragColor = u_color;
}
</script>

<!-- these are the point shaders -->
<script id="point-vertex" type="x-shader/x-vertex">
    uniform mat4 u_posmatrix;
    uniform vec2 u_size;
    uniform vec2 u_canvasSize;

    attribute vec2 a_pos;
    attribute vec2 a_extrude;
    varying vec2 imageCoords;

    const float offset = 0.5;
    const vec2 flipY = vec2(1.0, -1.0);

    // probably should handle this in vertexbuffer instead
    const float defaultExtrude = 63.0;

    void main(void) {
        vec2 dimensions = u_size/u_canvasSize * 2.0;
        vec4 transformed_pos = u_posmatrix * vec4(a_pos / 2.0, 0.0, 1.0);
        gl_Position = transformed_pos + vec4((a_extrude / defaultExtrude - offset) * flipY * dimensions, 0.0, 0.0);

        imageCoords = a_extrude / defaultExtrude;
    }

</script>
<script id="point-fragment" type="x-shader/x-fragment">
    precision mediump float;

    uniform sampler2D u_image;
    uniform vec2 u_size;

    varying vec2 imageCoords;

    void main(void) {
        gl_FragColor = texture2D(u_image, imageCoords);
    }

</script>


<!-- these are the shaders for rendering filled areas -->
<script id="area-vertex" type="x-shader/x-vertex">
attribute vec2 a_pos;

uniform mat4 u_posmatrix;

void main() {
    gl_Position = u_posmatrix * vec4(floor(a_pos / 2.0), 0, 1);
    gl_PointSize = 2.0;
}
</script>
<script id="area-fragment" type="x-shader/x-fragment">
precision mediump float;

uniform vec4 u_color;

void main() {
    gl_FragColor = u_color;

}
</script>

<!-- these are the shaders for rendering antialiased lines -->
<script id="line-vertex" type="x-shader/x-vertex">

// floor(127 / 2) == 63.0
// the maximum allowed miter limit is 2.0 at the moment. the extrude normal is
// stored in a byte (-128..127). we scale regular normals up to length 63, but
// there are also "special" normals that have a bigger length (of up to 126 in
// this case).
#define scale 63.0

attribute vec2 a_pos;
attribute vec2 a_extrude;

// posmatrix is for the vertex position, exmatrix is for rotating and projecting
// the extrusion vector.
uniform mat4 u_posmatrix;
uniform mat4 u_exmatrix;

uniform float u_debug;
uniform vec2 u_linewidth;
uniform vec4 u_color;

varying vec2 v_normal;

void main() {
    // We store the texture normals in the most insignificant bit
    // transform y so that 0 => -1 and 1 => 1
    // In the texture normal, x is 0 if the normal points straight up/down and 1 if it's a round cap
    // y is 1 if the normal points up, and -1 if it points down
    vec2 normal = mod(a_pos, 2.0);
    normal.y = sign(normal.y - 0.5);
    v_normal = normal;

    // Scale the extrusion vector down to a normal and then up by the line width
    // of this vertex.
    vec2 extrude = a_extrude / scale;
    vec2 dist = u_linewidth.s * extrude;

    // If the x coordinate is the maximum integer, we move the z coordinates out
    // of the view plane so that the triangle gets clipped. This makes it easier
    // for us to create degenerate triangle strips.
    float z = step(32767.0, a_pos.x);

    // Remove the texture normal bit of the position before scaling it with the
    // model/view matrix. Add the extrusion vector *after* the model/view matrix
    // because we're extruding the line in pixel space, regardless of the current
    // tile's zoom level.
    gl_Position = u_posmatrix * vec4(floor(a_pos / 2.0), 0, 1) + u_exmatrix * vec4(dist, z, 0);
}
</script>

<script id="line-fragment" type="x-shader/x-fragment">
precision mediump float;

uniform float u_debug;
uniform vec2 u_linewidth;
uniform vec4 u_color;

varying vec2 v_normal;

void main() {
    // Calculate the distance of the pixel from the line in pixels.
    float dist = length(v_normal) * u_linewidth.s;

    // Calculate the antialiasing fade factor. This is either when fading in
    // the line in case of an offset line (v_linewidth.t) or when fading out
    // (v_linewidth.s)
    float alpha = clamp(min(dist - (u_linewidth.t - 1.0), u_linewidth.s - dist), 0.0, 1.0);

    gl_FragColor = vec4(u_color.rgb, u_color.a * alpha);

    if (u_debug > 0.0) {
        gl_FragColor = vec4(1, 0, 0, 1);
    }
}
</script>

<!-- these are the shaders for rendering labels -->
<script id="label-vertex" type="x-shader/x-vertex">
attribute vec2 a_pos;
attribute vec2 a_offset;
attribute vec2 a_tex;

uniform vec2 u_texsize;
uniform mat4 u_posmatrix;
uniform mat4 u_resizematrix;

varying vec2 v_tex;

void main() {
    vec2 a = a_offset;
    gl_Position = u_posmatrix * vec4(a_pos, 0, 1) + u_resizematrix * vec4(a_offset, 0, 1);
    v_tex = a_tex / u_texsize;
}
</script>

<script id="label-fragment" type="x-shader/x-fragment">
precision mediump float;

uniform sampler2D u_sampler;
uniform vec4 u_color;
varying vec2 v_tex;

void main() {
    vec4 c = texture2D(u_sampler, v_tex);
    gl_FragColor = vec4(u_color.r, u_color.g, u_color.b, u_color.a * c.a);
}
</script>

</head>
<body>

<div id="map"></div>

</body>
</html>
